/*
Copyright (c) 2012, Intel Corporation

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

    * Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
    * Neither the name of Intel Corporation nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/
#version 140

in vec3 in_pos;

out vec3 vs_color;
out vec2 vs_tex;

uniform mat4 projectionMatrix;
uniform mat4 inverseMatrix;

uniform samplerBuffer particlePosition;

#define RADIUS 5

#define NUM_COLORS 5
uniform vec3 g_colors[NUM_COLORS] = vec3[NUM_COLORS]
(
    vec3(0.18, 0, 0.25),
    vec3(0.1, 0.4, 1),
    vec3(1, 0.2, 0),
    vec3(1, 1, 0.2),
    vec3(1, 1, 1)
);

vec3 ComputeColor(float in_speed)
{
    // FIXME?? surprising how slow the particles are...
    float x = in_speed * 100;

    if (x > 0.999f) x = 0.999f;

    int segment = int(x * (NUM_COLORS - 1));
    const float delta = 1.0f / (NUM_COLORS - 1);
    float x0 = segment * delta;
    float x1 = x0 + delta;
    float denominator = 1.0f / (x1 - x0);
    float c0 = (x1 - x) * denominator;
    float c1 = (x - x0) * denominator;

    vec3 c = (g_colors[segment]*c0) + (g_colors[segment+1]*c1);
    return c;
}

void main()
{
    int particleIndex = int(in_pos.z);
    vec4 particle = texelFetch(particlePosition, int(in_pos.z));

    vec4 quad = vec4(RADIUS*in_pos.xy,0,0);
    vec4 pos = (inverseMatrix*quad) + vec4(particle.xyz,1);
    gl_Position = projectionMatrix * pos;

    vs_color = ComputeColor(particle.w);
    vs_tex = (in_pos.xy + vec2(1,1)) * 0.5f;
}
